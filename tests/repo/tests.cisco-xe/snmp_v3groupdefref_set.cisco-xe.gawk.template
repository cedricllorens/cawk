#%SED_GAWK_PATH%

# ------------------------------------------------------------
# cawk is subjet to a MIT open-source licence
# please refer to the MIT licence file for further information
#
# for %SED_VAR% change like SED_GAWK_PATH, etc. please refer to 
# file support/tests.sed for further information
#
# purpose : check when a <snmp v3 group> is def and not ref
# purpose : check when a <snmp v3 group> is ref and not def
# author  : cedric llorens
# ------------------------------------------------------------

@include %SED_INCLUDE_PATH%

BEGIN {
}

BEGINFILE {
	pass = 1;
	delete snmp_def;
	delete snmp_ref;
}

# def ----

/^snmp-server group \w+ v3/ {
	snmp_def[$3] = $0;
	next;
}

# ref ----

/snmp-server user \w+ \w+ v3/ {
	snmp_ref[$4] = $0;
	next;
}

ENDFILE {

	# snmp def and not ref
	for (id in snmp_def) {
		if ( !(id in snmp_ref) ) {
			print_err("snmp_v3groupdefref_set.cisco-xe.gawk",id" : def not ref :"snmp_def[id],0,"low","error");
			pass = 0;
		}
	}

	# snmp ref and not def
	for (id in snmp_ref) {
		if ( !(id in snmp_def) ) {
			print_err("snmp_v3groupdefref_set.cisco-xe.gawk",id" : ref not def :"snmp_ref[id],0,"high","error");
			pass = 0;
		}
	}

	if ( pass ) print_err("snmp_v3groupdefref_set.cisco-xe.gawk","all snmp v3 groups defs are refs and the reverse",0,"high","pass");
}

END {
}
