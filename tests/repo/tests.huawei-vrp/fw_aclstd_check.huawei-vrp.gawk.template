#%SED_GAWK_PATH%

# ------------------------------------------------------------
# cawk is subjet to a MIT open-source licence
# please refer to the MIT licence file for further information
#
# for %SED_VAR% change like GAWK_PATH, etc. please refer to
# file support/tests.sed for further information
#
# purpose : check that <ip acl number standard ...> have inconsistent/redundant rules
# author  : cedric llorens
# ------------------------------------------------------------

# file format ----------------------------------
# test_name        : string
# test_description : string
# action           : permit/deny
# src@             : ip
# src_net@         : cird/netmask	set "none","host","any",[0-9]+,[0-9]+.[0-9]+.[0-9]+.[0-9]+
# port_src         : port_src		set 0 if empty
# port_src_range   : port_src_range	set 0 if empty
# dst@             : ip			set 0 if empty
# dst_net@         : cird/netmask	set "none","host","any",[0-9]+,[0-9]+.[0-9]+.[0-9]+.[0-9]+
# port_dst         : port_dst		set 0 if empty
# port_dst_range   : port_dst_range	set 0 if empty
# protocol         : protocol		set "none" if empty
# file format ----------------------------------

@include %SED_INCLUDE_PATH%
@include %SED_INCLUDE_PATH_FWLIB%

BEGIN {
}

BEGINFILE {
	cawk_init_fw_rules();
	block_acl = 0;
	acl = "";
	check = 0;
}

/^acl number [^ ]+$/ {
	check = 1;
	if ( acl != $3 && acl != "" ) {
		if ( cawk_fw_rules_assessment() ) {
			print_err("fw_aclstd_check.huawei-vrp.gawk","acl number "acl" has no redundant/inconsistent rules",FNR,"info","pass");
		}
		cawk_init_fw_rules();
	}
	acl = $3;
	block_acl = 1;
}

/^[ ]rule[ ][0-9]+ deny$/ && block_acl == 1 {
	cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,0,"any",0,0,0,0,0,0,"none")
	next;
}

/^[ ]rule[ ][0-9]+ (permit|deny) source [0-9\.]+ [0-9\.]+$/ && block_acl == 1 {
	if ( $6 != 0 ) {
		cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,$5,cawk_netMaskInv($6),0,0,0,0,0,0,"none")
	} else {
		cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,$5,"host",0,0,0,0,0,0,"none")
	}
	next;
}

/^[ ]rule[ ][0-9]+ (permit|deny) [^ ]+ source [0-9\.]+ [0-9\.]+$/ && block_acl == 1 {
	if ( $7 != 0 ) {
		cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,$6,cawk_netMaskInv($7),0,0,0,0,0,0,$4)
	} else {
		cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,$6,"host",0,0,0,0,0,0,$4)
	}
	next;
}

/^[ ]rule[ ][0-9]+ (permit|deny) [^ ]+ source [0-9\.]+ [0-9\.]+ destination [0-9\.]+ [0-9\.]+$/ && block_acl == 1 {
	if ( $7 != 0 ) {
		if ( $9 != 0 ) {
			cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,$6,cawk_netMaskInv($7),0,0,$9,cawk_netMaskInv($10),0,0,$4)
		} else {
			cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,$6,cawk_netMaskInv($7),0,0,$9,"host",0,0,$4)
		}
	} else {
		if ( $10 != 0 ) {
			cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,$6,"host",0,0,$9,cawk_netMaskInv($10),0,0,$4)
		} else {
			cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,$6,"host",0,0,$9,"host",0,0,$4)
		}
	}
	next;
}

/^[ ]rule[ ][0-9]+ (permit|deny) [^ ]+ source [0-9\.]+ [0-9\.]+ destination any$/ && block_acl == 1 {
	if ( $7 != 0 ) {
		cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,$6,cawk_netMaskInv($7),0,0,0,"any",0,0,$4)
	} else {
		cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,$6,"host",0,0,0,"any",0,0,$4)
	}
	next;
}

/^[ ]rule[ ][0-9]+ (permit|deny) [^ ]+ source any destination [0-9\.]+ [0-9\.]+$/ && block_acl == 1 {
	if ( $9 != 0 ) {
		cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,0,"any",0,0,$8,cawk_netMaskInv($9),0,0,$4)
	} else {
		cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,0,"any",0,0,$8,"any",0,0,$4)
	}
	next;
}

/^[ ]rule[ ][0-9]+ (permit|deny) [^ ]+ source any destination any$/ && block_acl == 1 {
	cawk_add_fw_rules("fw_aclstd_check.huawei-vrp.gawk","acl number "acl,$3,0,"any",0,0,0,"any",0,0,$4)
}

/^#$/ && block_acl == 1 {
	if ( cawk_fw_rules_assessment() ) {
		print_err("fw_aclstd_check.huawei-vrp.gawk","acl number "acl" has no redundant/inconsistent rules",FNR,"info","pass");
	}
	cawk_init_fw_rules();
	block_acl = 0;
	acl = "";
}

ENDFILE { 
	if ( check == 0 ) print_err("fw_aclstd_check.huawei-vrp.gawk","no <acl number [0-9]*> found",FNR,"info","pass");
}
