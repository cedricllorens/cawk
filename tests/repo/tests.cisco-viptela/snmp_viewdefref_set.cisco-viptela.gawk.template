#%SED_GAWK_PATH%

# ------------------------------------------------------------
# cawk is subjet to a MIT open-source licence
# please refer to the MIT licence file for further information
#
# for %SED_VAR% change like SED_GAWK_PATH, etc. please refer to 
# file support/tests.sed for further information
#
# purpose : check when a <snmp view> is def and not ref
# purpose : check when a <snmp view> is ref and not def
# author  : cedric llorens
# ------------------------------------------------------------

@include %SED_INCLUDE_PATH%

BEGIN {
}

BEGINFILE {
	pass = 1;
	delete snmp_def;
	delete snmp_ref;
	block_snmp = 0;
}

/^snmp/ {
	block_snmp = 1;
}

# def ----

/^[ ]view [^ ]+/ && block_snmp == 1 {
	snmp_def[$2] = $0;
	next;
}

# ref ----

/^[ ][ ]view [^ ]+/ && block_snmp == 1 {
	snmp_ref[$2] = $0;
	next;
}

/^!$/ && block_snmp == 1 {
	block_snmp = 0;
	next;
}

ENDFILE {

	# snmp def and not ref
	for (id in snmp_def) {
		if ( !(id in snmp_ref) ) {
			print_err("snmp_viewdefref_set.cisco-viptela.gawk",id" : def not ref :"snmp_def[id],0,"low","error");
			pass = 0;
		}
	}

	# snmp ref and not def
	for (id in snmp_ref) {
		if ( !(id in snmp_def) ) {
			print_err("snmp_viewdefref_set.cisco-viptela.gawk",id" : ref not def :"snmp_ref[id],0,"high","error");
			pass = 0;
		}
	}

	if ( pass ) print_err("snmp_viewdefref_set.cisco-viptela.gawk","all snmps defs are refs and the reverse",0,"high","pass");
}

END {
}
