#%SED_GAWK_PATH%

# ------------------------------------------------------------
# cawk is subjet to a MIT open-source licence
# please refer to the MIT licence file for further information
#
# for %SED_VAR% change like GAWK_PATH, etc. please refer to
# file support/tests.sed for further information
#
# purpose : provide rule information
# author  : adrien lebout
# ------------------------------------------------------------

@include %SED_INCLUDE_PATH%

# local functions ----
function processrule(rule) {

    if ( rule ~ /^(pass|block) (in|out )?all$/ ) {
      print_err("global_ruleinfo_set.packetfilter-fwcli.gawk","blacklist/whitelist rule detected : "rule,FNR,"low","warning");
      pass = 0;
    }

    if ( rule ~ /^(pass|block) (in|out )?(inet|inet6) / ) {
      print_err("global_ruleinfo_set.packetfilter-fwcli.gawk","permissive rule detected : "rule,FNR,"low","warning");
      pass = 0;
    }

    if (rule ~ /^pass / && (rule ~ / (from|to) 0\.0\.0\.0\/0/ || rule ~ / (from|to) ::\/0/)) {
        print_err("global_ruleinfo_set.packetfilter-fwcli.gawk", "rule with wide IP range <<"rule">>", FNR, "info", "warning");
    }

    if (rule ~ /^pass / && rule !~ / proto /) {
        print_err("global_ruleinfo_set.packetfilter-fwcli.gawk", "rule without protocol specification <<"rule">>", FNR, "info", "warning");
    }

    if (rule ~ /^pass / && rule !~ / port /) {
        print_err("global_ruleinfo_set.packetfilter-fwcli.gawk", "rule without port specification <<"rule">>", FNR, "info", "warning");
    }

    if (rule ~ /accept/ && rule ~ /quick/ && (rule ~ / from any/ || rule ~ / to any/ || rule !~ / proto / || rule !~ / port /)) {
        print_err("global_quick_misuse.checkpoint-fwcli.gawk", "quick used in overly permissive rule <<"rule">>", FNR, "info", "warning");
    }

    if (rule ~ /log/ && rule ~ /quick/) {
        print_err("global_quick_with_log.checkpoint-fwcli.gawk", "quick used in logging rule <<"rule">>", FNR, "info", "warning");
    }

}
# local functions ----

BEGIN {
}

BEGINFILE {
  pass = 1;
  rule = "";
}

# std line ----
/^(pass|block) / && !/\\$/{
  processrule($0);
  next;
}
# std line ----

# multi line rule ----
/^(pass|block) / && /\\$/ && rule == "" {
    rule = rule $0;
    next;
}

/\\$/ && rule != "" {
    rule = rule $0;
    next;
}

!/\\$/ && rule != "" {
    rule = rule $0;
    processrule(rule);
    rule = "";
    next;
}
# multi line rule ----

ENDFILE {
  if ( pass )  {
	  print_err("global_ruleinfo_set.packetfilter-fwcli.gawk","no permissive rules detected",FNR,"info","pass");
  }
}

END {
}
