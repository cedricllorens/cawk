#%SED_GAWK_PATH%

# ------------------------------------------------------------
# cawk is subjet to a MIT open-source licence
# please refer to the MIT licence file for further information
#
#
# for %SED_VAR% change like GAWK_PATH, etc. please refer to
# file support/tests.sed for further information
#
# purpose : check that <duplicate rules> are not set
# author  : lucas vanhaaren
# ------------------------------------------------------------

@include %SED_INCLUDE_PATH%

# local functions ----
function processrule(rule,RULES) {
    if ( !(rule in RULES)) {
        RULES[rule] = 1
    } else {
        print_err("global_duplicaterule_notset.checkpoint-fwcli.gawk","duplicate rule detected <<"rule">>",FNR,"medium","error");
        pass = 0;
    }
}
# local functions ----

BEGIN {
}

BEGINFILE {
    delete RULES;
    pass = 1;
    rule = "";
}

# std rule ----
/^(add )?(access-|nat-)?rule / && !/\\$/ && rule == "" {
    processrule($0,RULES);
    next;
}
# std rule ----

# multi line rule ----
/^(add )?(access-|nat-)?rule / && /\\$/ && rule == "" {
    rule = rule $0;
    next;
}

/\\$/ && rule != "" {
    rule = rule $0;
    next;
}

!/\\$/ && rule != "" {
    rule = rule $0;
    processrule(rule,RULES);
    rule = "";
    next;
}
# multi line rule ----

ENDFILE {
    if ( pass ) {
        print_err("global_duplicaterule_notset.checkpoint-fwcli.gawk","no duplicate rules detected",FNR,"medium","pass");
    }
}

END {
}
