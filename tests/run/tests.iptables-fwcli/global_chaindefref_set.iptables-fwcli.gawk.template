#%SED_GAWK_PATH%

# ------------------------------------------------------------
# cawk is subjet to a MIT open-source licence
# please refer to the MIT licence file for further information
#
#
# for %SED_VAR% change like GAWK_PATH, etc. please refer to
# file support/tests.sed for further information
#
# purpose : check <-N hains> ref not def and def not ref
# author  : wael elsingaby
# ------------------------------------------------------------

@include %SED_INCLUDE_PATH%

BEGIN {
    delete default_chains;

    # if it is a false positive you can add what you want there
    default_chains["ACCEPT"] = 1;
    default_chains["DROP"] = 1;
    default_chains["REJECT"] = 1;
    default_chains["RETURN"] = 1;
    default_chains["LOG"] = 1;
    default_chains["SNAT"] = 1;
    default_chains["DNAT"] = 1;
    default_chains["MASQUERADE"] = 1;
    default_chains["REDIRECT"] = 1;
    default_chains["MARK"] = 1;
    default_chains["DSCP"] = 1;
    default_chains["NOTRACK"] = 1;
    default_chains["SECMARK"] = 1;
    default_chains["CONNSECMARK"] = 1;
}

BEGINFILE {
    delete chains_defined;
    delete chains_referenced;
}

# chain def ----
/ -N / {
    chain_name = $3;
    chains_defined[chain_name] = NR;
}

# chain ref -----
/ -j \w+/ {
    match($0, /-j ([A-Za-z0-9_\-]+)/, arr);
    if (arr[1] != "" && !(arr[1] in default_chains)) {
        chain_name = arr[1];
        chains_referenced[chain_name] = NR; 
    }
}

ENDFILE {

    # chain def not ref -----
    pass = 1;
    for (chain in chains_defined) {
        if (!(chain in chains_referenced)) {
            print_err("global_chaindefref_set.iptables-fwcli.gawk","var def not ref <<"chain">>",0,"low","error");
            pass = 0;
        }
    }
    if ( pass ) {
        print_err("global_chaindefref_set.iptables-fwcli.gawk","no chain def not ref",0,"low","pass");
    }

    # chain ref not def ----
    pass = 1;
    for (chain in chains_referenced) {
        if (!(chain in chains_defined)) {
            print_err("global_chaindefref_set.iptables-fwcli.gawk","var ref not def <<"chain">>",0,"high","error");
            pass = 0;
        }
    }
    if ( pass ) {
        print_err("global_chaindefref_set.iptables-fwcli.gawk","no chain ref not def",0,"high","pass");
    }
}

END {
}
