#%SED_GAWK_PATH%

# ------------------------------------------------------------
# cawk is subjet to a MIT open-source licence
# please refer to the MIT licence file for further information
#
# for %SED_VAR% change like SED_GAWK_PATH, etc. please refer to 
# file support/tests.sed for further information
#
# purpose : check when a <snmp view> is def and not ref
# purpose : check when a <snmp view> is ref and not def
# author  : cedric llorens
# ------------------------------------------------------------

@include %SED_INCLUDE_PATH%

BEGIN {
}

BEGINFILE {
	pass = 1;
	delete snmp_def;
	delete snmp_ref;
}

# def ----

/^snmp-server view [^ ]+/ {
	snmp_def[$3] = $0;
	next;
}

# ref ----

/^snmp.* view .*/ && !/^snmp-server view/ {
	for(i=1;i<=NF;i++) {
		if ( $i == "view" ) {
			snmp_ref[$(i+1)] = $0;
			break;
		}
	}
	next;
}

/^snmp-server group .*$/ {
	for(i=1;i<=NF;i++) {
		if ( $i == "read" && $(i+1) != "write" && $(i+1) != "notify" && $(i+1) != "access" ) {
			snmp_ref[$(i+1)] = $0;
		}
		if ( $i == "write" && $(i+1) != "notify" && $(i+1) != "access" ) {
			snmp_ref[$(i+1)] = $0;
		}
	}
	next;
}

ENDFILE {

	# snmp def and not ref
	for (id in snmp_def) {
		if ( !(id in snmp_ref) ) {
			print_err("snmp_viewdefref_set.cisco-xe.gawk",id" : def not ref :"snmp_def[id],0,"info","error");
			pass = 0;
		}
	}

	# snmp ref and not def
	for (id in snmp_ref) {
		if ( !(id in snmp_def) ) {
			print_err("snmp_viewdefref_set.cisco-xe.gawk",id" : ref not def :"snmp_ref[id],0,"high","error");
			pass = 0;
		}
	}

	if ( pass ) print_err("snmp_viewdefref_set.cisco-xe.gawk","all snmps defs are refs and the reverse",0,"high","pass");
}

END {
}
